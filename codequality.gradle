subprojects {
    // Checkstyle ===========================================================

    apply plugin: 'checkstyle'

    checkstyle {
        config = resources.text.fromFile("$rootProject.projectDir/build-tools/src/main/resources/checkstyle/checkstyle.xml")
        configProperties = ['package_name': 'common']
        ignoreFailures = false
        showViolations = true
    }

    checkstyleMain.onlyIf { project.hasProperty('codeQuality') }
    checkstyleMain.source = "src/main/java" // Explicitly disable generated code
    checkstyleTest.enabled = false

    // PMD ==================================================================

    apply plugin: 'pmd'

    pmd {
        ignoreFailures = false
        ruleSetFiles = rootProject.files("$rootProject.projectDir/build-tools/src/main/resources/pmd/pmdRuleSet.xml")
        ruleSets = []
        sourceSets = [ sourceSets.main ]
    }

    pmdMain.onlyIf { project.hasProperty('codeQuality') }
    pmdTest.enabled = false

    // Errorprone ===========================================================

    apply plugin: 'net.ltgt.errorprone-base'

    // This makes sure only production code (not tests) is checked
    if (project.hasProperty('codeQuality')) {
        compileJava {
            toolChain net.ltgt.gradle.errorprone.ErrorProneToolChain.create(project)
            options.compilerArgs += [ '-Xep:TypeParameterUnusedInFormals:ERROR', '-Xep:MissingOverride:ERROR', '-Xep:DefaultCharset:ERROR',
                                      '-Xep:ClassCanBeStatic:ERROR', '-Xep:Finally:ERROR' ]
        }
    }

    // Jacoco ===============================================================

    apply plugin: 'jacoco'

    jacoco {
        toolVersion = "0.7.6.201602180812"
    }

    test {
        jacoco {
            destinationFile = file("$buildDir/jacoco/test.exec")
        }
    }

    // License ==============================================================

    apply plugin: 'com.github.hierynomus.license'

    license {
        ext.project_name = "sklintyg"
        ext.project_url = "https://github.com/sklintyg"
        ext.year = Calendar.getInstance().get(Calendar.YEAR)
        strictCheck true
        header file("$rootProject.projectDir/build-tools/src/main/resources/license/header.txt")
        mapping("java","SLASHSTAR_STYLE")
        include "**/*.java"
    }

    licenseMain.onlyIf { project.hasProperty('codeQuality') }
    licenseTest.onlyIf { project.hasProperty('codeQuality') }
}

apply plugin: "org.sonarqube"

sonarqube {
    properties {
        property "sonar.jacoco.reportPath", "$buildDir/jacoco/test.exec"
        property "sonar.projectName", "Intyg Common"
        property "sonar.projectKey", "intyg-common"
        property "sonar.host.url", System.properties['sonarUrl'] ?: "https://build-inera.nordicmedtest.se/sonar"
        property "sonar.test.exclusions", "src/test/**"
        property "sonar.exclusions", ["**/stub/**", "**/exception/**", "**/*Exception*.java", "**/templates.js", "**/vendor/**"]
        property "sonar.javascript.lcov.reportPath", "build/karma/merged_lcov.info"
    }
}

// This buildscript block should not be required, since we have one in build.gradle, but for some reason errorprone
// only works if the block is present in this file as well.
buildscript {
    dependencies {
        classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.8'
    }

    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
}
