ext {
    commonVersion = System.properties['commonVersion'] ?: '0-SNAPSHOT'

    intygCertificateSchemasVersion = "1.0.4"
    intygTransportstyrelsenSchemasVersion = "1.0.4"
    rivtaInsuranceprocessSchemasVersion = "3.1.0.4"

    autovalueVersion = "1.1"
    commonsCollectionsVersion = "3.2.1"
    commonsIoVersion = "2.4"
    fkEIntygAdapterVersion = "2.2-RC2-INTYG"
    itextVersion = "5.3.2"
    phSchematronVersion = "3.0.1"
    schematronVersion = "1.0.4"
    servletApiVersion = "2.5"
    springVersion = "4.2.2.RELEASE"

    jsonAssertVersion = "1.2.0"
    mockitoVersion = "1.9.0"
    unitilsCoreVersion = "3.3"
    xmlUnitVersion = "1.4"
}

allprojects {
    group = 'se.inera.intyg.intygstyper'
    version = System.properties['buildVersion'] ?: '0-SNAPSHOT'

    apply plugin: 'inera-intyg'
    apply plugin: 'maven'
    apply plugin: 'org.ajoberstar.grgit'
}

subprojects {
    apply plugin: 'java'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    task createVersionPropertyFile(type: VersionPropertyFileTask)

    task unzipBuildTools(type: Copy) {
        description 'Extracts common-build-tools'
        into "$buildDir/build-tools/"
        from {
            configurations.buildtools.collect { zipTree(it) }
        }
    }

    task packageTests(type: Jar) {
        from sourceSets.test.output
        classifier = 'tests'
    }
    artifacts.archives packageTests

    apply plugin: 'checkstyle'

    checkstyle {
        config = resources.text.fromFile("${buildDir}/build-tools/checkstyle/checkstyle.xml")
        ignoreFailures = false
        showViolations = true
    }

    checkstyleMain.onlyIf { project.hasProperty('codeQuality') }
    checkstyleMain.dependsOn unzipBuildTools
    checkstyleMain.source = "src/main/java"
    checkstyleTest.enabled = false

    apply plugin: 'jacoco'

    jacoco {
        toolVersion = "0.7.6.201602180812"
    }

    test {
        jacoco {
            destinationFile = file("$buildDir/jacoco/test.exec")
        }
    }

    uploadArchives.repositories.mavenDeployer {
        repository(url: "https://build-inera.nordicmedtest.se/nexus/content/repositories/releases") {
            authentication(userName: System.properties['nexusUsername'], password: System.properties['nexusPassword'])
        }
    }

    [install.repositories.mavenInstaller, uploadArchives.repositories.mavenDeployer]*.pom*.whenConfigured { pom ->
        pom.dependencies.findAll { dep -> dep.groupId == 'se.inera.intyg.common' }.each {
            it.version = findResolvedVersion(project, 'se.inera.intyg.common')
        }
    }

    configurations {
        buildtools
    }

    dependencies {
        compile "se.inera.intyg.common:common-support:${commonVersion}"
        compile "se.inera.intyg.common:common-web:${commonVersion}"
        compile "se.inera.intyg.common:integration-util:${commonVersion}"
        compile "se.inera.intyg.common:common-services:${commonVersion}"
        compile "se.inera.intyg.common:common-schemas:${commonVersion}"

        compile "com.itextpdf:itextpdf:${itextVersion}"
        compile "commons-collections:commons-collections:${commonsCollectionsVersion}"
        compile "com.helger:ph-schematron:${phSchematronVersion}"
        compile "org.springframework:spring-tx:${springVersion}"

        buildtools "se.inera.intyg.common:common-build-tools:${commonVersion}"

        testCompile "junit:junit:4.+"
        testCompile "commons-io:commons-io:${commonsIoVersion}"
        testCompile "org.mockito:mockito-core:${mockitoVersion}"
        testCompile "org.skyscreamer:jsonassert:${jsonAssertVersion}"
        testCompile "org.springframework:spring-test:${springVersion}"
        testCompile "org.unitils:unitils-core:${unitilsCoreVersion}"
        testCompile "xmlunit:xmlunit:${xmlUnitVersion}"
    }

    repositories {
        mavenLocal()
        maven { url "https://build-inera.nordicmedtest.se/nexus/content/repositories/releases/" }
        maven { url "http://repo.maven.apache.org/maven2" }
    }
}

configure(subprojects.findAll {['fk7263', 'ts-bas', 'ts-diabetes', 'lisjp', 'luse', 'luae_fs', 'luae_na'].contains(it.name)}) {
    apply plugin: 'com.moowork.grunt'

    grunt {
        colors = System.properties['gruntColors'] ? System.properties['gruntColors'].toBoolean() : true
    }

    node {
        version = '6.6.0'
        download = true
        distBaseUrl = 'https://build-inera.nordicmedtest.se/node/'
        workDir = rootProject.projectDir
        nodeModulesDir = rootProject.projectDir
    }

    task jsInclude(type: NodeTask, dependsOn: [npmInstall, unzipBuildTools]) {
        def webappOutputDir = 'build/'

        inputs.dir("$projectDir/src/main/resources")
        inputs.files(fileTree(projectDir).include('*.js'))
        inputs.files(fileTree(projectDir).include('*.json'))
        inputs.files(fileTree(rootProject.projectDir).include('package.json'))
        inputs.files(configurations.compile)
        outputs.dir(webappOutputDir)

        script = file("${rootProject.projectDir}/node_modules/grunt-cli/bin/grunt")
        args = ["--gruntfile=${rootProject.projectDir}/Gruntfile.js", "--base=${projectDir}", "--module=${project.name}",
                "--outputDir=${webappOutputDir}", "--npmDir=${rootProject.projectDir}", 'default']
    }

    jar.dependsOn(jsInclude)

    task jsCodeQuality(type: NodeTask, dependsOn: [npmInstall]) {
        outputs.upToDateWhen { false }

        script = file("${rootProject.projectDir}/node_modules/grunt-cli/bin/grunt")
        args = ["--gruntfile=${rootProject.projectDir}/Gruntfile.js", "--base=${projectDir}", "--module=${project.name}",
                "--npmDir=${rootProject.projectDir}", 'lint']
    }

    checkstyleMain.dependsOn(jsCodeQuality)
    jsCodeQuality.onlyIf { project.hasProperty('codeQuality') }

    jar {
        exclude '**/*.scss'
        exclude '**/*.spec.js'
        exclude '**/*.test.js'
        exclude '**/karma*.conf.js'
        exclude '**/karma*.conf.ci.js'
    }
}

configure(subprojects.findAll { ['fk7263', 'ts-bas', 'ts-diabetes'].contains(it.name) }) {
    task unzipWebjars(type: Copy) {
        description 'Copies common-web from the classpath and expands it so that we can use their static resources with karma'
        into "$buildDir/webjars/"
        from {
            configurations.jstests.collect { zipTree(it) }
        }
    }

    task jsTests(type: NodeTask, dependsOn: [npmInstall, unzipWebjars]) {
        def webappTestOutputDir = 'build/karma/'

        inputs.dir("${projectDir}/src/main/resources")
        inputs.files(fileTree(projectDir).include('*.js'))
        inputs.files(fileTree(projectDir).include('*.json'))
        inputs.files(fileTree(rootProject.projectDir).include('package.json'))
        inputs.files(configurations.compile)
        outputs.dir(webappTestOutputDir)

        script = file("${rootProject.projectDir}/node_modules/grunt-cli/bin/grunt")
        args = ["--gruntfile=${rootProject.projectDir}/Gruntfile.js", "--base=${projectDir}", "--module=${project.name}",
                "--outputDir=${webappTestOutputDir}", "--npmDir=${rootProject.projectDir}", 'test']
    }

    test.dependsOn(jsTests)

    configurations {
        jstests
    }

    dependencies {
        jstests "se.inera.intyg.common:common-web:${commonVersion}"
    }
}

task tagRelease(type: TagReleaseTask)

task testReport(type: SharedTestReportTask)

apply plugin: "org.sonarqube"

sonarqube {
    properties {
        property "sonar.jacoco.reportPath", "$buildDir/jacoco/test.exec"
        property "sonar.projectName", "Intygstyper"
        property "sonar.projectKey", "intygstyper"
        property "sonar.host.url", System.properties['sonarUrl'] ?: "https://build-inera.nordicmedtest.se/sonar"
        property "sonar.test.exclusions", "src/test/**"
        property "sonar.exclusions", ["**/stub/**", "**/exception/**", "**/*Exception*.java", "**/templates.js"]
        property "sonar.javascript.lcov.reportPath", "build/karma/merged_lcov.info"
    }
}

buildscript {
    dependencies {
        classpath 'se.inera.intyg:gradle-intyg-plugin:1.0.+'
        classpath 'com.moowork.gradle:gradle-node-plugin:0.+'
        classpath 'com.moowork.gradle:gradle-grunt-plugin:0.+'
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.+"
        classpath 'org.ajoberstar:gradle-git:1.+'
    }

    repositories {
        mavenLocal()
        maven { url "https://build-inera.nordicmedtest.se/nexus/content/repositories/releases/" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
}
