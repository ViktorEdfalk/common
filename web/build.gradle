description = 'Intyg Common (Web)'

apply plugin: 'com.moowork.grunt'
apply plugin: 'com.moowork.node'

def webappOutputDir = 'build/';
def webappTestOutputDir = 'build/karma/';

grunt {
  colors = true
}

node {
  version = '0.12.4'
  download = true
  distBaseUrl = 'https://build-inera.nordicmedtest.se/node/'
}

sourceSets {
  main {
    resources {
      exclude '**/*.scss'
      exclude '**/*.spec.js'
    }
  }
}

task jsTests(type: NodeTask, dependsOn: [npmInstall]) {
  inputs.dir("$projectDir/src/main/resources")
  inputs.file("$projectDir/Grunfile.js")
  inputs.file("$projectDir/bower.json")
  outputs.dir(webappTestOutputDir)

  script = file('node_modules/grunt-cli/bin/grunt')
  args = ['test', '--outputDir=' + webappTestOutputDir, '--skip-coverage='+(!project.hasProperty('runSonar'))]
}

test.dependsOn(jsTests)

task jsInclude(type: NodeTask, dependsOn: [npmInstall]) {
  inputs.dir("$projectDir/src/main/resources")
  inputs.file("$projectDir/Grunfile.js")
  inputs.file("$projectDir/bower.json")
  outputs.dir(webappOutputDir)

  script = file('node_modules/grunt-cli/bin/grunt')
  args = ['default', '--outputDir=' + webappOutputDir]
}

jar.dependsOn(jsInclude)

buildscript {
  repositories {
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
  }

  dependencies {
    classpath 'com.moowork.gradle:gradle-node-plugin:0.12'
    classpath 'com.moowork.gradle:gradle-grunt-plugin:0.12'
  }
}
